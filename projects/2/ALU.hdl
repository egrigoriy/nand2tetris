// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
	// pre-set x
	Mux16(a=x, b=false, sel=zx, out=xzx);
	Not16(in=xzx, out=notxzx);
	Mux16(a=xzx, b=notxzx, sel=nx, out=xpre);
	// pre-set y
	Mux16(a=y, b=false, sel=zy, out=yzy);
	Not16(in=yzy, out=notyzy);
	Mux16(a=yzy, b=notyzy, sel=ny, out=ypre);
	// select f
	Add16(a=xpre, b=ypre, out=fadd);
	And16(a=xpre, b=ypre, out=fand);
	Mux16(a=fand, b=fadd, sel=f, out=fxy);
	// post-set output
	Not16(in=fxy, out=notfxy);
	Mux16(a=fxy, b=notfxy, sel=no, out=out, out[0..7]=lsbout, out[8..15]=msbout, out[15]=ng);
	// zr
	Or8Way(in=lsbout, out=lsbzr);
	Or8Way(in=msbout,out=msbzr);
	Or(a=lsbzr,b=msbzr,out=notzr);
	Not(in=notzr,out=zr);
}